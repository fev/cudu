buildscript {
  ext {
    springBootVersion = '1.3.0.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.flywaydb:flyway-gradle-plugin:3.2.1")
  }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.flywaydb.flyway'

jar {
  baseName = 'cudu'
  version =  '2.3.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url 'http://repo.spring.io/milestone'
  }
}

springBoot {
  executable = true
}

configurations {
  providedRuntime
  jpaMetaModel
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-security")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-data-jpa")
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")
  compile("org.springframework.boot:spring-boot-starter-mail")
  compile("org.springframework.boot:spring-boot-starter-jooq")

  compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
  compile("com.fasterxml.jackson.datatype:jackson-datatype-hibernate4")

  compile("com.google.guava:guava:18.0")
  compile("org.apache.pdfbox:pdfbox:1.8.9")
  compile("org.zeroturnaround:zt-zip:1.8")

  compile("com.h2database:h2")
  compile("org.hibernate:hibernate-validator")
//  compile("org.postgresql:postgresql:9.4-1202-jdbc42")
  compile("org.postgresql:postgresql:42.2.8")

  jpaMetaModel("org.hibernate:hibernate-jpamodelgen")

  testCompile("org.springframework.boot:spring-boot-starter-test")
}

sourceSets {
  generated.java.srcDirs = ['generated/src/main/java']
  main {
    java {
      srcDir 'generated/src/main/java'
      srcDir 'src/main/java'
    }
  }
}

clean {
  delete "generated"
}

task generateJpaMetaModel(type: JavaCompile, group: 'build', description: 'JPA MetaModel Generation') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.jpaMetaModel
  options.compilerArgs = ["-proc:only"]
  destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava.dependsOn generateJpaMetaModel
ideaProject.dependsOn generateJpaMetaModel
compileJava.source sourceSets.generated.java, sourceSets.main.java

task wrapper(type: Wrapper) {
  gradleVersion = '2.7'
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
  project {
    vcs = 'Git'
  }
}

eclipse {
  classpath {
    containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
    containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
  }
}
